delimiter $ ( [ { ~ $
          $ } ] ) , $;
strict provable sort wff;
term imp (p q: wff): wff; infixr imp: $->$ prec 25;
term not (p: wff): wff; prefix not: $~$ prec 41;

axiom ax1 (a b: wff): $ a -> b -> a $;
axiom ax2 (a b c: wff): $ (a -> b -> c) -> (a -> b) -> a -> c $;
axiom ax3 (a b: wff): $ (~a -> ~b) -> b -> a $;
axiom ax_mp (a b: wff): $ a -> b $ > $ a $ > $ b $;

theorem a1i (h: $ b $): $ a -> b $ = '(ax_mp ax1 h);
theorem a2i (h: $ a -> b -> c $): $ (a -> b) -> (a -> c) $ = '(ax_mp ax2 h);
theorem mpd (h1: $ a -> b $) (h2: $ a -> b -> c $): $ a -> c $ = '(ax_mp (ax_mp ax2 h2) h1);
theorem mpi (h1: $ b $) (h2: $ a -> b -> c $): $ a -> c $ = '(mpd (a1i h1) h2);
theorem id: $ a -> a $ = '(mpd (! ax1 _ a) ax1);
theorem idd: $ a -> b -> b $ = '(a1i id);
theorem syl (h1: $ b -> c $) (h2: $ a -> b $): $ a -> c $ = '(mpd h2 (a1i h1));
theorem rsyl (h1: $ a -> b $) (h2: $ b -> c $): $ a -> c $ = '(syl h2 h1);
theorem a1d (h: $ a -> b $): $ a -> c -> b $ = '(syl ax1 h);
theorem a2d (h: $ a -> b -> c -> d $): $ a -> (b -> c) -> (b -> d) $ = '(syl ax2 h);
theorem a3d (h: $ a -> ~b -> ~c $): $ a -> c -> b $ = '(syl ax3 h);
theorem sylc (h: $ b -> c -> d $) (h1: $ a -> b $) (h2: $ a -> c $): $ a -> d $ = '(mpd h2 (syl h h1));
theorem syld (h1: $ a -> b -> c $) (h2: $ a -> c -> d $): $ a -> b -> d $ = '(mpd h1 (a2d (a1d h2)));
theorem syl5 (h1: $ b -> c $) (h2: $ a -> c -> d $): $ a -> b -> d $ = '(syld (a1i h1) h2);
theorem syl6 (h1: $ c -> d $) (h2: $ a -> b -> c $): $ a -> b -> d $ = '(syld h2 (a1i h1));
theorem imim2: $ (b -> c) -> (a -> b) -> (a -> c) $ = '(a2d ax1);
theorem imim2i (h: $ b -> c $): $ (a -> b) -> (a -> c) $ = '(ax_mp imim2 h);
theorem imim2d (h: $ a -> c -> d $): $ a -> (b -> c) -> (b -> d) $ = '(a2d (a1d h));
theorem absurd: $ ~a -> a -> b $ = '(a3d ax1);
theorem com12 (h: $ a -> b -> c $): $ b -> a -> c $ = '(syl (a2i h) ax1);
theorem mpcom: $ a -> (a -> b) -> b $ = '(com12 id);
theorem com23 (h: $ a -> b -> c -> d $): $ a -> c -> b -> d $ = '(syl (com12 (imim2d mpcom)) h);
theorem eimd (h1: $ a -> b $) (h2: $ a -> c -> d $): $ a -> (b -> c) -> d $ = '(syld (rsyl h1 mpcom) h2);
theorem absurdr: $ a -> ~a -> b $ = '(com12 absurd);
theorem imim1: $ (a -> b) -> (b -> c) -> (a -> c) $ = '(com12 imim2);
theorem imim1i (h: $ a -> b $): $ (b -> c) -> (a -> c) $ = '(ax_mp imim1 h);
theorem imim1d (h: $ a -> b -> c $): $ a -> (c -> d) -> (b -> d) $ = '(syl imim1 h);
theorem imimd (h1: $ a -> b -> c $) (h2: $ a -> d -> e $): $ a -> (c -> d) -> (b -> e) $ = '(syld (imim1d h1) (imim2d h2));
theorem imim: $ (a -> b) -> (c -> d) -> (b -> c) -> (a -> d) $ = '(syl5 (imim2) (imim2d imim1));
theorem imimii (h1: $ a -> b $) (h2: $ c -> d $) : $(b -> c) -> (a -> d)$ = '(ax_mp (ax_mp imim h1) h2);
theorem imidm: $ (a -> a -> b) -> a -> b $ = '(a2i mpcom);
theorem eim: $ a -> (b -> c) -> (a -> b) -> c $ = '(imim1d mpcom);
theorem contra: $ (~a -> a) -> a $ = '(ax_mp imidm (a3d (a2i absurd)));
theorem dne: $ ~~a -> a $ = '(syl contra absurd);
theorem inot: $ (a -> ~a) -> ~a $ = '(syl contra (imim1i dne));
theorem con2: $ (a -> ~b) -> (b -> ~a) $ = '(syl ax3 (imim1i dne));
theorem con2i (h: $ a -> ~b $) : $ b -> ~a $ = '(ax_mp con2 h);
theorem notnot1: $ a -> ~~a $ = '(ax_mp con2 id);
theorem con3: $ (a -> b) -> (~b -> ~a) $ = '(syl con2 (imim2i notnot1));
theorem con3i (h: $ a -> b $) : $ ~b -> ~a $ = '(ax_mp con3 h);
theorem con1: $ (~a -> b) -> (~b -> a) $ = '(syl ax3 (imim2i notnot1));
theorem con1i (h: $ ~a -> b $) : $ ~b -> a $ = '(ax_mp con1 h);
theorem cases (h1: $ a -> b $) (h2: $ ~a -> b $): $ b $ = '(ax_mp contra (syl h2 (ax_mp con3 h1)));
theorem casesd (h1: $ a -> b -> c $) (h2: $ a -> ~b -> c $): $ a -> c $ =
'(cases (com12 h1) (com12 h2));
theorem con1d (h: $ a -> ~b -> c $): $ a -> ~c -> b $ = '(syl con1 h);
theorem con2d (h: $ a -> b -> ~c $): $ a -> c -> ~b $ = '(syl con2 h);
theorem con3d (h: $ a -> b -> c $): $ a -> ~c -> ~b $ = '(syl con3 h);
theorem con4d (h: $ a -> ~b -> ~c $): $ a -> c -> b $ = '(syl ax3 h);
theorem mt (h1: $ b -> a $) (h2: $ ~a $): $ ~b $ = '(ax_mp (ax_mp con3 h1) h2);
theorem mt2 (h1: $ b -> ~a $) (h2: $ a $): $ ~b $ = '(ax_mp (ax_mp con2 h1) h2);
theorem mtd (h1: $ a -> ~b $) (h2: $ a -> c -> b $): $ a -> ~c $ = '(mpd h1 (con3d h2));
theorem mti (h1: $ ~b $) (h2: $ a -> c -> b $): $ a -> ~c $ = '(mpi h1 (con3d h2));
theorem mt2d (h1: $ a -> c $) (h2: $ a -> b -> ~c $): $ a -> ~b $ = '(mpd h1 (con2d h2));

def and (a b: wff): wff = $ ~(a -> ~b) $; infixl and: $/\$ prec 34;

theorem anl: $ a /\ b -> a $ = '(ax_mp con1 absurd);
theorem anr: $ a /\ b -> b $ = '(ax_mp con1 ax1);
theorem anli (h: $ a /\ b $): $ a $ = '(ax_mp anl h);
theorem anri (h: $ a /\ b $): $ b $ = '(ax_mp anr h);
theorem ian: $ a -> b -> a /\ b $ = '(con2d mpcom);
theorem iand (h1: $ a -> b $) (h2: $ a -> c $): $ a -> b /\ c $ = '(sylc ian h1 h2);
theorem anld (h: $ a -> b /\ c $): $ a -> b $ = '(syl anl h);
theorem anrd (h: $ a -> b /\ c $): $ a -> c $ = '(syl anr h);
theorem iani (h1: $ a $) (h2: $ b $): $ a /\ b $ = '(ax_mp (ax_mp ian h1) h2);
theorem anwl (h: $ a -> c $): $ a /\ b -> c $ = '(syl h anl); 
theorem anwr (h: $ b -> c $): $ a /\ b -> c $ = '(syl h anr);
theorem anll: $ a /\ b /\ c -> a $ = '(syl anl anl);
theorem anlr: $ a /\ b /\ c -> b $ = '(syl anr anl);
theorem anrl: $ a /\ (b /\ c) -> b $ = '(syl anl anr);
theorem anrr: $ a /\ (b /\ c) -> c $ = '(syl anr anr);
theorem anwll (h: $ a -> d $): $ a /\ b /\ c -> d $ = '(syl h anll);
theorem anw3l (h: $ a -> e $): $ a /\ b /\ c /\ d -> e $ = '(syl h (syl anl anll));
theorem anw4l (h: $ a -> f $): $ a /\ b /\ c /\ d /\ e -> f $ = '(syl h (syl (anw3l id) anl));
theorem anw5l (h: $ a -> g $): $ a /\ b /\ c /\ d /\ e /\ f -> g $ = '(syl h (syl (anw4l id) anl));
theorem anw6l (x: $ a -> h $): $ a /\ b /\ c /\ d /\ e /\ f /\ g -> h $ = '(syl x (syl (anw5l id) anl));
theorem anw7l (x: $ a -> i $): $ a /\ b /\ c /\ d /\ e /\ f /\ g /\ h -> i $ = '(syl x (syl (anw6l id) anl));
theorem anllr: $ a /\ b /\ c /\ d -> b $ = '(syl (syl anr anl) anl);
theorem an3l: $ a /\ b /\ c /\ d -> a $ = '(syl (syl anl anl) anl);
theorem an3lr: $ a /\ b /\ c /\ d /\ e -> b $ = '(syl (syl (syl anr anl) anl) anl);
theorem an4l: $ a /\ b /\ c /\ d /\ e -> a $ = '(syl an3l anl); -- TODO: automate these
theorem an4lr: $ a /\ b /\ c /\ d /\ e /\ f -> b $ = '(syl an3lr anl);
theorem an5l: $ a /\ b /\ c /\ d /\ e /\ f -> a $ = '(syl an4l anl);
theorem an5lr: $ a /\ b /\ c /\ d /\ e /\ f /\ g -> b $ = '(syl an4lr anl);
theorem an6l: $ a /\ b /\ c /\ d /\ e /\ f /\ g -> a $ = '(syl an5l anl);
theorem an6lr: $ a /\ b /\ c /\ d /\ e /\ f /\ g /\ h -> b $ = '(syl an5lr anl);
theorem imp_and (h: $ a -> b -> c $): $ a /\ b -> c $ = '(sylc h anl anr);
theorem exp (h: $ a /\ b -> c $): $ a -> b -> c $ = '(syl6 h ian);
theorem impcom (h: $ a -> b -> c $): $ b /\ a -> c $ = '(imp_and (com12 h));
theorem expcom (h: $ a /\ b -> c $): $ b -> a -> c $ = '(com12 (exp h));
theorem syla (h1: $ (b -> c) -> d $) (h2: $ a /\ b -> c $): $ a -> d $ = '(syl h1 (exp h2));
theorem sylan (h: $ b /\ c -> d $) (h1: $ a -> b $) (h2: $ a -> c $): $ a -> d $ = '(sylc (exp h) h1 h2);
theorem animd (h1: $ a -> b -> c $) (h2: $ a -> d -> e $): $ a -> b /\ d -> c /\ e $ =
'(exp (iand (imp_and (syl5 anl h1)) (imp_and (syl5 anr h2))));
theorem anim1d (h: $ a -> b -> c $): $ a -> b /\ d -> c /\ d $ = '(animd h idd);
theorem anim2d (h: $ a -> c -> d $): $ a -> b /\ c -> b /\ d $ = '(animd idd h);
theorem anim1: $ (a -> b) -> a /\ c -> b /\ c $ = '(animd id idd);
theorem anim1i (h: $a -> b$): $ a /\ c -> b /\ c $ = '(ax_mp anim1 h);
theorem anim2: $ (b -> c) -> a /\ b -> a /\ c $ = '(animd idd id);
theorem anim2i (h: $b -> c$): $ a /\ b -> a /\ c $ = '(ax_mp anim2 h);
theorem anim: $ (a -> b) -> (c -> d) -> a /\ c -> b /\ d $ = '(com23 (syl6 (com12 anim2) anim1));
theorem animii (h1: $ a -> b $) (h2: $ c -> d $): $ a /\ c -> b /\ d $ = '(ax_mp (ax_mp anim h1) h2);
theorem anim2a: $ (a -> b -> c) -> (a /\ b -> a /\ c) $ = '(con3d (syl ax2 (imim2i con3)));
theorem anim2ai (h : $ a -> b -> c $) : $a /\ b -> a /\ c $ = '(ax_mp anim2a h);
theorem ancom: $ a /\ b -> b /\ a $ = '(con3i con2);
theorem anrasss (h: $ a /\ b /\ c -> d $): $ a /\ c /\ b -> d $ = '(sylan h (iand anll anr) anlr);
theorem anim1a: $ (a -> b -> c) -> (b /\ a -> c /\ a) $ = '(syl6 ancom (syl5 ancom anim2a));
theorem anim1ai (h : $ a -> b -> c $) : $b /\ a -> c /\ a $ = '(ax_mp anim1a h);
theorem casesda (h1: $ a /\ b -> c $) (h2: $ a /\ ~b -> c $): $ a -> c $ =
'(casesd (syl6 h1 ian) (syl6 h2 ian));
theorem inotda (h: $ a /\ b -> ~b $): $ a -> ~b $ = '(syla inot h);
theorem mpand (h1: $ a -> b $) (h2: $ a /\ b -> c $): $ a -> c $ = '(sylan h2 id h1);
theorem mtand (h1: $ a -> ~b $) (h2: $ a /\ c -> b $): $ a -> ~c $ = '(mtd h1 (exp h2));
theorem mtani (h1: $ ~b $) (h2: $ a /\ c -> b $): $ a -> ~c $ = '(mtand (a1i h1) h2);
theorem and_assoc : $ (a /\ b /\ c) -> (a /\ (b /\ c)) $ = '(iand anll (iand anlr anr));
theorem and_associ (h: $ a /\ b /\ c $) : $ a /\ (b /\ c) $ = '(ax_mp and_assoc h);
theorem and_assocr : $ (a /\ (b /\ c)) -> (a /\ b /\ c) $ = '(iand (iand anl (syl anl anr)) (syl anr anr));
theorem and_assocri (h: $ a /\ (b /\ c) $) : $ a /\ b /\ c $ = '(ax_mp and_assocr h);
theorem and_com12 : $ a /\ (b /\ c) -> b /\ (a /\ c) $ = '(syl and_assoc (syl (anim1i ancom) and_assocr));
theorem and_com23 : $ a /\ b /\ c -> a /\ c /\ b $ = '(syl (syl ancom and_com12) (and_assoc));
theorem and_ax2 : $ (a -> b /\ c) -> (a -> b) /\ (a -> c) $ = '(iand (imim2i anl) (imim2i anr));
theorem and_ax2r : $ (a -> b) /\ (a -> c) -> (a -> b /\ c) $ = '(com12 (animd mpcom mpcom));
theorem expde : $ (a /\ b -> c) -> (a -> b -> c) $ = '(exp (exp (mpd (syl anr and_assoc) anll)));
theorem expder : $ (a -> b -> c) -> (a /\ b -> c) $ = '(exp (mpd anrr (mpd anrl anl)));

def iff (a b: wff): wff = $ (a -> b) /\ (b -> a) $; infixl iff: $<->$ prec 20;

theorem bi1: $ (a <-> b) -> a -> b $ = '(anl);
theorem bi1i (h: $ a <-> b $): $ a -> b $ = '(ax_mp bi1 h);
theorem bi1d (h: $ a -> (b <-> c) $): $ a -> b -> c $ = '(syl bi1 h);
theorem bi1a (h: $ a -> (b <-> c) $): $ a /\ b -> c $ = '(impcom (com12 (bi1d h)));
theorem bi2: $ (a <-> b) -> b -> a $ = '(anr);
theorem bi2i (h: $ a <-> b $): $ b -> a $ = '(ax_mp bi2 h);
theorem bi2d (h: $ a -> (b <-> c) $): $ a -> c -> b $ = '(syl bi2 h);
theorem bi2a (h: $ a -> (b <-> c) $): $ a /\ c -> b $ = '(impcom (com12 (bi2d h)));
theorem ibii (h1: $ a -> b $) (h2: $ b -> a $): $ a <-> b $ = '(iani h1 h2);
theorem ibid (h1: $ a -> b -> c $) (h2: $ a -> c -> b $): $ a -> (b <-> c) $ = '(sylc ian h1 h2);
theorem ibida (h1: $ a /\ b -> c $) (h2: $ a /\ c -> b $): $ a -> (b <-> c) $ = '(ibid (exp h1) (exp h2));
theorem biid: $ a <-> a $ = '(ibii id id);
theorem biidd: $ a -> (b <-> b) $ = '(ibid idd idd);
theorem mpbi (h1: $ a <-> b $) (h2: $ a $): $ b $ = '(ax_mp (bi1i h1) h2);
theorem mpbir (h1: $ b <-> a $) (h2: $ a $): $ b $ = '(ax_mp (bi2i h1) h2);
theorem mpbid (h1: $ a -> (b <-> c) $) (h2: $ a -> b $): $ a -> c $ = '(sylc bi1 h1 h2);
theorem mpbird (h1: $ a -> (c <-> b) $) (h2: $ a -> b $): $ a -> c $ = '(sylc bi2 h1 h2);
theorem mpbii (h1: $ b $) (h2: $ a -> (b <-> c) $): $ a -> c $ = '(mpi h1 (bi1d h2));
theorem mpbiri (h1: $ b $) (h2: $ a -> (c <-> b) $): $ a -> c $ = '(mpi h1 (bi2d h2));
theorem mtbi (h1: $ a <-> b $) (h2: $ ~a $): $ ~b $ = '(ax_mp (con3i (bi2i h1)) h2);
theorem mtbir (h1: $ b <-> a $) (h2: $ ~a $): $ ~b $ = '(ax_mp (con3i (bi1i h1)) h2);
theorem mtbid (h1: $ a -> (b <-> c) $) (h2: $ a -> ~b $): $ a -> ~c $ = '(mpd h2 (con3d (bi2d h1)));
theorem mtbird (h1: $ a -> (c <-> b) $) (h2: $ a -> ~b $): $ a -> ~c $ = '(mpd h2 (con3d (bi1d h1)));
theorem con1b: $ (~a <-> b) -> (~b <-> a) $ = '(ibid (con1d anl) (con2d anr));
theorem con2b: $ (a <-> ~b) -> (b <-> ~a) $ = '(ibid (con2d bi1) (con1d bi2));
theorem con3b: $ (a <-> b) -> (~a <-> ~b) $ = '(ibid (con3d bi2) (con3d bi1));
theorem con4b: $ (~a <-> ~b) -> (a <-> b) $ = '(ibid (con4d bi2) (con4d bi1));
theorem con1bb: $ (~a <-> b) <-> (~b <-> a) $ = '(ibii con1b con1b);
theorem con2bb: $ (a <-> ~b) <-> (b <-> ~a) $ = '(ibii con2b con2b);
theorem con3bb: $ (a <-> b) <-> (~a <-> ~b) $ = '(ibii con3b con4b);
theorem con1bi: $ (~a -> b) <-> (~b -> a) $ = '(ibii con1 con1);
theorem con2bi: $ (a -> ~b) <-> (b -> ~a) $ = '(ibii con2 con2);
theorem con3bi: $ (a -> b) <-> (~b -> ~a) $ = '(ibii con3 ax3);
theorem notnot: $ a <-> ~~a $ = '(ibii notnot1 dne);
theorem bithd (h1: $ a -> b $) (h2: $ a -> c $): $ a -> (b <-> c) $ = '(ibid (syl ax1 h2) (syl ax1 h1));
theorem binthd (h1: $ a -> ~b $) (h2: $ a -> ~c $): $ a -> (b <-> c) $ = '(ibid (a3d (syl ax1 h1)) (a3d (syl ax1 h2)));
theorem bith: $ a -> b -> (a <-> b) $ = '(exp (bithd anl anr));
theorem binth: $ ~a -> ~b -> (a <-> b) $ = '(exp (binthd anl anr));
theorem bicom: $ (a <-> b) -> (b <-> a) $ = '(sylc ian bi2 bi1);
theorem bicomi (h: $ (a <-> b) $): $ (b <-> a) $ = '(ax_mp bicom h);
theorem bicomb: $ (a <-> b) <-> (b <-> a) $ = '(ibii bicom bicom);
theorem bicomd (h: $ a -> (b <-> c) $): $ a -> (c <-> b) $ = '(syl bicom h);
theorem bitrd (h1: $ a -> (b <-> c) $) (h2: $ a -> (c <-> d) $): $ a -> (b <-> d) $ =
'(ibid (syld (bi1d h1) (bi1d h2)) (syld (bi2d h2) (bi2d h1)));
theorem bitr2d (h1: $ a -> (b <-> c) $) (h2: $ a -> (c <-> d) $): $ a -> (d <-> b) $ = '(bicomd (bitrd h1 h2));
theorem bitr3d (h1: $ a -> (c <-> b) $) (h2: $ a -> (c <-> d) $): $ a -> (b <-> d) $ = '(bitrd (bicomd h1) h2);
theorem bitr4d (h1: $ a -> (b <-> c) $) (h2: $ a -> (d <-> c) $): $ a -> (b <-> d) $ = '(bitrd h1 (bicomd h2));
theorem bitr: $ (a <-> b) -> (b <-> c) -> (a <-> c) $ = '(exp (bitrd anl anr));
theorem bitr2: $ (a <-> b) -> (b <-> c) -> (c <-> a) $ = '(syl6 bicom bitr);
theorem bitr3: $ (b <-> a) -> (b <-> c) -> (a <-> c) $ = '(syl bitr bicom);
theorem bitr4: $ (a <-> b) -> (c <-> b) -> (a <-> c) $ = '(syl5 bicom bitr);
theorem bitrii (h1: $ a <-> b $) (h2: $ b <-> c $) : $ a <-> c $ = '(ax_mp (ax_mp bitr h1) h2);
theorem sylib (h1: $ b <-> c $) (h2: $ a -> b $): $ a -> c $ = '(syl (bi1i h1) h2);
theorem sylibr (h1: $ c <-> b $) (h2: $ a -> b $): $ a -> c $ = '(syl (bi2i h1) h2);
theorem sylbi (h1: $ a <-> b $) (h2: $ b -> c $): $ a -> c $ = '(syl h2 (bi1i h1));
theorem sylbir (h1: $ b <-> a $) (h2: $ b -> c $): $ a -> c $ = '(syl h2 (bi2i h1));
theorem syl5bb (h1: $ b <-> c $) (h2: $ a -> (c <-> d) $): $ a -> (b <-> d) $ = '(syl (ax_mp bitr h1) h2);
theorem syl5bbr (h1: $ c <-> b $) (h2: $ a -> (c <-> d) $): $ a -> (b <-> d) $ = '(syl (ax_mp bitr3 h1) h2);
theorem syl6bb (h1: $ c <-> d $) (h2: $ a -> (b <-> c) $): $ a -> (b <-> d) $ = '(syl (mpi h1 bitr) h2);
theorem syl6bbr (h1: $ d <-> c $) (h2: $ a -> (b <-> c) $): $ a -> (b <-> d) $ = '(syl (mpi h1 bitr4) h2);
theorem syl5bi (h1: $ b <-> c $) (h2: $ a -> c -> d $): $ a -> b -> d $ = '(syl5 (bi1i h1) h2);
theorem syl5bir (h1: $ c <-> b $) (h2: $ a -> c -> d $): $ a -> b -> d $ = '(syl5 (bi2i h1) h2);
theorem syl6ib (h1: $ c <-> d $) (h2: $ a -> b -> c $): $ a -> b -> d $ = '(syl6 (bi1i h1) h2);
theorem syl6ibr (h1: $ d <-> c $) (h2: $ a -> b -> c $): $ a -> b -> d $ = '(syl6 (bi2i h1) h2);
theorem syl5ibrcom (h1: $ c -> (b <-> d) $) (h2: $ a -> d $): $ a -> c -> b $ = '(syl5 h1 (syl (com12 bi2) h2));
theorem sylbid (h1: $ a -> (b <-> c) $) (h2: $ a -> c -> d $): $ a -> b -> d $ = '(syld (bi1d h1) h2);
theorem sylibd (h1: $ a -> b -> c $) (h2: $ a -> (c <-> d) $): $ a -> b -> d $ = '(syld h1 (bi1d h2));
theorem sylbird (h1: $ a -> (c <-> b) $) (h2: $ a -> c -> d $): $ a -> b -> d $ = '(syld (bi2d h1) h2);
theorem sylibrd (h1: $ a -> b -> c $) (h2: $ a -> (d <-> c) $): $ a -> b -> d $ = '(syld h1 (bi2d h2));
theorem bitr3g (h1: $ b <-> d $) (h2: $ c <-> e $) (h: $ a -> (b <-> c) $):
  $ a -> (d <-> e) $ = '(syl6bb h2 (syl5bb (bicomi h1) h));
theorem bitr4g (h1: $ d <-> b $) (h2: $ e <-> c $) (h: $ a -> (b <-> c) $):
  $ a -> (d <-> e) $ = '(syl6bb (bicomi h2) (syl5bb h1 h));
theorem bitr3gi (h1: $ a <-> c $) (h2: $ b <-> d $) (h: $ a <-> b $): $ c <-> d $ = '(bitrii (bitrii (bicomi h1) h) h2);
theorem bitr4gi (h1: $ c <-> a $) (h2: $ d <-> b $) (h: $ a <-> b $): $ c <-> d $ = '(bitrii (bitrii h1 h) (bicomi h2));
theorem impbi (h: $ a -> (b <-> c) $): $ a /\ b -> c $ = '(bi1a h);
theorem impbir (h: $ a -> (c <-> b) $): $ a /\ b -> c $ = '(bi1a (bicomd h));
theorem ancomb: $ a /\ b <-> b /\ a $ = '(ibii ancom ancom);
theorem anass: $ a /\ b /\ c <-> a /\ (b /\ c) $ =
'(ibii and_assoc and_assocr);
theorem bian2a: $ (a -> b) -> (a /\ b <-> a) $ = '(ibid (a1i anl) (com12 (syld mpcom ian)));
theorem bian1a: $ (b -> a) -> (a /\ b <-> b) $ = '(ibid (a1i anr) (com12 (syld mpcom (com12 ian))));
theorem bian1: $ a -> (a /\ b <-> b) $ = '(ibid (a1i anr) ian);
theorem bian2: $ b -> (a /\ b <-> a) $ = '(ibid (a1i anl) (com12 ian));
theorem bibi1: $ a -> ((a <-> b) <-> b) $ = '(ibid (com12 bi1) bith);
theorem bibi2: $ b -> ((a <-> b) <-> a) $ = '(ibid (com12 bi2) (com12 bith));
theorem noteq: $ (a <-> b) -> (~a <-> ~b) $ = '(ibid (syl bi1 con3b) (syl bi2 con3b));
theorem noteqi (h: $ a <-> b $): $ ~a <-> ~b $ = '(ax_mp noteq h);
theorem noteqd (h: $ a -> (b <-> c) $): $ a -> (~b <-> ~c) $ = '(syl noteq h);
theorem imeqd
  (h1: $ a -> (b <-> c) $) (h2: $ a -> (d <-> e) $): $ a -> (b -> d <-> c -> e) $ =
'(ibid (imimd (bi2d h1) (bi1d h2)) (imimd (bi1d h1) (bi2d h2)));
theorem bibin1: $ ~a -> ((a <-> b) <-> ~b) $ = '(ibid (com12 (bi1d noteq)) binth);
theorem bibin2: $ ~b -> ((a <-> b) <-> ~a) $ = '(syl5bb bicomb bibin1);
theorem imeq1d (h: $ a -> (b <-> c) $): $ a -> (b -> d <-> c -> d) $ = '(imeqd h biidd);
theorem imeq2d (h: $ a -> (c <-> d) $): $ a -> (b -> c <-> b -> d) $ = '(imeqd biidd h);
theorem imeq1i (h: $ a <-> b $): $ a -> c <-> b -> c $ = '(ax_mp (imeq1d id) h);
theorem imeq2i (h: $ b <-> c $): $ a -> b <-> a -> c $ = '(ax_mp (imeq2d id) h);
theorem imeqi (h1: $ a <-> b $) (h2: $ c <-> d $): $ a -> c <-> b -> d $ = '(bitrii (imeq1i h1) (imeq2i h2));
theorem aneqd
  (h1: $ a -> (b <-> c) $) (h2: $ a -> (d <-> e) $): $ a -> (b /\ d <-> c /\ e) $ =
'(noteqd (imeqd h1 (noteqd h2)));
theorem imeq2a: $ (a -> (b <-> c)) -> (a -> b <-> a -> c) $ = '(ibid (syl ax2 (imim2i bi1)) (syl ax2 (imim2i bi2)));
theorem imeq1a: $ (~c -> (a <-> b)) -> (a -> c <-> b -> c) $ = '(bitr4g con3bi con3bi (syl imeq2a (imim2i noteq)));
theorem imeq2da (h: $ G /\ a -> (b <-> c) $): $ G -> (a -> b <-> a -> c) $ = '(syl imeq2a (exp h));
theorem aneq1d (h: $ a -> (b <-> c) $): $ a -> (b /\ d <-> c /\ d) $ = '(aneqd h biidd);
theorem aneq2d (h: $ a -> (c <-> d) $): $ a -> (b /\ c <-> b /\ d) $ = '(aneqd biidd h);
theorem aneq: $ (a <-> b) -> (c <-> d) -> (a /\ c <-> b /\ d) $ = '(exp (aneqd anl anr));
theorem aneqii (h1: $ a <-> b $) (h2: $ c <-> d$): $ a /\ c <-> b /\ d $ = '(ax_mp (ax_mp aneq h1) h2);
theorem aneq1i (h: $ a <-> b $): $ a /\ c <-> b /\ c $ = '(ax_mp (ax_mp aneq h) biid);
theorem aneq2i (h: $ b <-> c $): $ a /\ b <-> a /\ c $ = '(ax_mp (ax_mp aneq biid) h);
theorem aneq2a: $ (a -> (b <-> c)) -> (a /\ b <-> a /\ c) $ = '(syl noteq (syl imeq2a (imim2i noteq)));
theorem aneq1a: $ (c -> (a <-> b)) -> (a /\ c <-> b /\ c) $ = '(syl noteq (syl imeq1a (imim1i dne)));
theorem aneq1da (h: $ G /\ c -> (a <-> b) $): $ G -> (a /\ c <-> b /\ c) $ = '(syl aneq1a (exp h));
theorem aneq2da (h: $ G /\ a -> (b <-> c) $): $ G -> (a /\ b <-> a /\ c) $ = '(syl aneq2a (exp h));
theorem anlass: $ a /\ (b /\ c) <-> b /\ (a /\ c) $ = '(ibii and_com12 and_com12);
theorem anrass: $ a /\ b /\ c <-> a /\ c /\ b $ = '(ibii and_com23 and_com23);
theorem an4: $ (a /\ b) /\ (c /\ d) <-> (a /\ c) /\ (b /\ d) $ = '(ax_mp (ax_mp bitr4 anass) (bitrii anass (aneq2i anlass)));
theorem anroti (h: $ a -> b /\ d $): $ a /\ c -> b /\ c /\ d $ = '(syl and_com23 (anim1i h));
theorem anrotri (h: $ b /\ d -> a $): $ b /\ c /\ d -> a /\ c $ = '(syl (anim1i h) and_com23);
theorem bian11i (h: $ a <-> b /\ c $): $ a /\ d <-> b /\ (c /\ d) $ = '(bitrii (aneq1i h) anass);
theorem bian21i (h: $ a <-> b /\ c $): $ a /\ d <-> (b /\ d) /\ c $ = '(bitrii (aneq1i h) anrass);
theorem bian12i (h: $ a <-> b /\ c $): $ d /\ a <-> b /\ (d /\ c) $ = '(bitr3gi (ancomb) (aneq2i ancomb) (bian11i h));
theorem bian22i (h: $ a <-> b /\ c $): $ d /\ a <-> (d /\ b) /\ c $ = '(bitrii (aneq2i h) (bicomi anass));
theorem bian11d (h: $ G -> (a <-> b /\ c) $): $ G -> (a /\ d <-> b /\ (c /\ d)) $ = '(syl6bb anass (aneq1d h));
theorem bian21d (h: $ G -> (a <-> b /\ c) $): $ G -> (a /\ d <-> (b /\ d) /\ c) $ = '(syl6bb anrass (aneq1d h));
theorem bian12d (h: $ G -> (a <-> b /\ c) $): $ G -> (d /\ a <-> b /\ (d /\ c)) $ = '(syl6bb anlass (aneq2d h));
theorem bian22d (h: $ G -> (a <-> b /\ c) $): $ G -> (d /\ a <-> (d /\ b) /\ c) $ = '(syl6bb (bicomi anass) (aneq2d h));
theorem bian11da (h: $ G /\ d -> (a <-> b /\ c) $): $ G -> (a /\ d <-> b /\ (c /\ d)) $ = '(syl6bb anass (aneq1da h));
theorem bian21da (h: $ G /\ d -> (a <-> b /\ c) $): $ G -> (a /\ d <-> (b /\ d) /\ c) $ = '(syl6bb anrass (aneq1da h));
theorem bian12da (h: $ G /\ d -> (a <-> b /\ c) $): $ G -> (d /\ a <-> b /\ (d /\ c)) $ = '(syl6bb anlass (aneq2da h));
theorem bian22da (h: $ G /\ d -> (a <-> b /\ c) $): $ G -> (d /\ a <-> (d /\ b) /\ c) $ = '(syl6bb (bicomi anass) (aneq2da h));
theorem anidm: $ a /\ a <-> a $ = '(ibii anl (iand id id));
theorem anandi: $ a /\ (b /\ c) <-> (a /\ b) /\ (a /\ c) $ = '(bitrii (aneq1i (bicomi anidm)) an4);
theorem anandir: $ (a /\ b) /\ c <-> (a /\ c) /\ (b /\ c) $ = '(bitrii (aneq2i (bicomi anidm)) an4);
theorem imandi: $ (a -> b /\ c) <-> (a -> b) /\ (a -> c) $ = '(ibii and_ax2 and_ax2r);
theorem imancom: $ a /\ (b -> c) -> b -> a /\ c $ = '(com12 (animd idd mpcom));
theorem rbida (h1: $ a /\ c -> b $) (h2: $ a /\ d -> b $) (h: $ a /\ b -> (c <-> d) $): $ a -> (c <-> d) $ =
'(bitr3d (syla bian2a h1) (bitrd (syla aneq1a h) (syla bian2a h2)));
theorem rbid (h1: $ b -> a $) (h2: $ c -> a $) (h: $ a -> (b <-> c) $): $ b <-> c $ =
'(ax_mp (ax_mp bitr3 (ax_mp bian2a h1)) (ax_mp (ax_mp bitr (ax_mp aneq1a h)) (ax_mp bian2a h2)));
theorem bieqd
  (h1: $ a -> (b <-> c) $) (h2: $ a -> (d <-> e) $): $ a -> ((b <-> d) <-> (c <-> e)) $ =
'(aneqd (imeqd h1 h2) (imeqd h2 h1));
theorem bieq1d (h: $ a -> (b <-> c) $): $ a -> ((b <-> d) <-> (c <-> d)) $ = '(bieqd h biidd);
theorem bieq2d (h: $ a -> (c <-> d) $): $ a -> ((b <-> c) <-> (b <-> d)) $ = '(bieqd biidd h);
theorem bieq: $ (a <-> b) -> (c <-> d) -> ((a <-> c) <-> (b <-> d)) $ = '(exp (bieqd anl anr));
theorem bieq1: $ (a <-> b) -> ((a <-> c) <-> (b <-> c)) $ = '(bieqd id biidd);
theorem bieq2: $ (b <-> c) -> ((a <-> b) <-> (a <-> c)) $ = '(bieqd biidd id);
theorem impexp: $ (a /\ b -> c) <-> (a -> b -> c) $ = '(ibii expde expder);
theorem impd (h: $ a -> b -> c -> d $): $ a -> b /\ c -> d $ = '(syl expder h);
theorem expd (h: $ a -> b /\ c -> d $): $ a -> b -> c -> d $ = '(syl expde h);
theorem com12b: $ (a -> b -> c) <-> (b -> a -> c) $ = '(ibii (com23 id) (com23 id));


def or (a b: wff): wff = $ ~a -> b $; infixl or: $\/$ prec 30;

theorem orl: $ a -> a \/ b $ = '(absurdr);
theorem orr: $ b -> a \/ b $ = '(ax1);
theorem eori (h1: $ a -> c $) (h2: $ b -> c $): $ a \/ b -> c $ = '(casesd (a1i h1) (imim2i h2));
theorem eord (h1: $ a -> b -> d $) (h2: $ a -> c -> d $): $ a -> b \/ c -> d $ = '(com12 (eori (com12 h1) (com12 h2)));
theorem eorda (h1: $ a /\ b -> d $) (h2: $ a /\ c -> d $): $ a -> b \/ c -> d $ = '(eord (exp h1) (exp h2));
theorem orld (h: $ a -> b $): $ a -> b \/ c $ = '(syl orl h);
theorem orrd (h: $ a -> c $): $ a -> b \/ c $ = '(syl orr h);
theorem eor: $ (a -> c) -> (b -> c) -> a \/ b -> c $ = '(exp (eord anl anr));
theorem orimd (h1: $ a -> b -> c $) (h2: $ a -> d -> e $): $ a -> b \/ d -> c \/ e $ = '(imimd (con3d h1) h2);
theorem orim1d (h: $ a -> b -> c $): $ a -> b \/ d -> c \/ d $ = '(imim1d (con3d h));
theorem orim2d (h: $ a -> c -> d $): $ a -> b \/ c -> b \/ d $ = '(imim2d h);
theorem orim1: $ (a -> b) -> a \/ c -> b \/ c $ = '(imim1d con3);
theorem orim2: $ (b -> c) -> a \/ b -> a \/ c $ = '(imim2d id);
theorem oreq1i (h: $ a <-> b $): $ a \/ c <-> b \/ c $ = '(imeq1i (noteqi h));
theorem oreq2i (h: $ b <-> c $): $ a \/ b <-> a \/ c $ = '(imeq2i h);
theorem orim: $ (a -> b) -> (c -> d) -> a \/ c -> b \/ d $ = '(syl imim con3);
theorem oreqi (h1: $ a <-> b $) (h2: $ c <-> d $): $ a \/ c <-> b \/ d $ = '(imeqi (noteqi h1) h2);
theorem oreqid (h1: $ a -> (b <-> c) $) (h2: $ a -> (d <-> e) $): $ a -> (b \/ d <-> c \/ e) $ = 
'(ibid (orimd (bi1d h1) (bi1d h2)) (orimd (bi2d h1) (bi2d h2)));
theorem orcom: $ a \/ b -> b \/ a $ = '(con1);
theorem orcomb: $ a \/ b <-> b \/ a $ = '(con1bi);
theorem or12: $ a \/ (b \/ c) <-> b \/ (a \/ c) $ = '(com12b);
theorem orass: $ a \/ b \/ c <-> a \/ (b \/ c) $ = '(bitrii con1bi (bitrii com12b (oreq2i con1bi)));
theorem or4: $ (a \/ b) \/ (c \/ d) <-> (a \/ c) \/ (b \/ d) $ = '(bitrii orass (bitrii (imeq2i or12) (bicomi orass)));
theorem oridm: $ a \/ a <-> a $ = '(ibii contra absurdr);
theorem notan2: $ ~(a /\ b) <-> a -> ~b $ = '(bitrii (bicomi notnot) biid);
theorem notan: $ ~(a /\ b) <-> (~a \/ ~b) $ = '(bitrii (bicomi notnot) (imeq1i notnot));
theorem notor: $ ~(a \/ b) <-> (~a /\ ~b) $ = '(ax_mp con3b (imeq2i notnot));
theorem iman: $ a -> b <-> ~(a /\ ~b) $ = '(bitrii (imeq2i notnot) notnot);
theorem imorim: $((a \/ b) -> c) -> (a -> c) /\ (b -> c)$ = '(iand (imim1i orl) (imim1i orr));
theorem imorimr: $ (a -> c) /\ (b -> c) -> ((a \/ b) -> c)$ = '(imp_and eor);
theorem imor: $ ((a \/ b) -> c) <-> ((a -> c) /\ (b -> c)) $ = '(ibii imorim imorimr);
theorem andi: $ a /\ (b \/ c) <-> a /\ b \/ a /\ c $ = '(ibii (imp_and (orimd ian ian)) (eori (anim2i orl) (anim2i orr)));
theorem andir: $ (a \/ b) /\ c <-> a /\ c \/ b /\ c $ = '(bitrii ancomb (bitrii andi (oreqi ancomb ancomb)));
theorem ordi: $ a \/ (b /\ c) <-> (a \/ b) /\ (a \/ c) $ = '(ibii (eori (iand orl orl) (animii orr orr)) and_ax2r);
theorem ordir: $ (a /\ b) \/ c <-> (a \/ c) /\ (b \/ c) $ = '(bitrii orcomb (bitrii ordi (aneqii orcomb orcomb)));
theorem oreq2a: $ (a \/ (b <-> c)) -> (a \/ b <-> a \/ c) $ = '(imeq2a);
theorem oreq1a: $ (c \/ (a <-> b)) -> (a \/ c <-> b \/ c) $ = '(syl imeq1a (imim2i con3b));
theorem biim1a: $ (a \/ b) -> (a -> b <-> b) $ = '(ibid (exp (casesd anr anl)) (a1i ax1));
theorem biim2a: $ (b -> ~a) -> (a -> b <-> ~a) $ = '(ibid (exp (casesd anl (syl con3 anr))) (a1i absurd));
theorem bior1a: $ (a -> b) -> (a \/ b <-> b) $ = '(ibid (exp (casesd anl anr)) (a1i orr));
theorem bior2a: $ (b -> a) -> (a \/ b <-> a) $ = '(ibid (exp (casesd anl (syl con1 anr))) (a1i absurdr));
theorem biim1: $ a -> (a -> b <-> b) $ = '(ibid mpcom (a1i ax1));
theorem biim2: $ ~b -> (a -> b <-> ~a) $ = '(ibid (com12 con3) (a1i absurd));
theorem bior1: $ ~a -> (a \/ b <-> b) $ = '(ibid mpcom (a1i ax1));
theorem bior2: $ ~b -> (a \/ b <-> a) $ = '(ibid (com12 con1) (a1i absurdr));
theorem em: $ p \/ ~p $ = '(id);
theorem emr: $ ~p \/ p $ = '(dne);


--| Selection: `ifp p a b` is equivalent to `a` if `p` is true,
--| and equivalent to `b` if `p` is false.
def ifp (p a b: wff): wff = $ p /\ a \/ ~p /\ b $;

theorem ifppos (p a b: wff): $ p -> (ifp p a b <-> a) $ = '(bitrd (syl bior2 (con2i anl)) bian1);
theorem ifpneg (p a b: wff): $ ~p -> (ifp p a b <-> b) $ = '(bitrd (syl (bior1) (con3i anl)) bian1);
theorem ifpid: $ ifp p a a <-> a $ = '(cases ifppos ifpneg);
theorem ifpnot: $ ifp (~p) a b <-> ifp p b a $ = '(bitrii (oreq2i (aneq1i (bicomi notnot))) orcomb);
theorem ifpan1: $ ifp p a b /\ c <-> ifp p (a /\ c) (b /\ c) $ = '(bitrii andir (oreqi anass anass));
theorem ifpan2: $ c /\ ifp p a b <-> ifp p (c /\ a) (c /\ b) $ = '(bitrii andi (oreqi anlass anlass));
theorem ifpor: $ ifp p (a \/ b) (c \/ d) <-> ifp p a c \/ ifp p b d $ = '(bitrii (oreqi andi andi) or4);
theorem ifpeq2a: $ (p -> (a1 <-> a2)) -> (ifp p a1 b <-> ifp p a2 b) $ = '(syl oreq1a (syl orr aneq2a));
theorem ifpeq3a: $ (~p -> (b1 <-> b2)) -> (ifp p a b1 <-> ifp p a b2) $ = '(syl oreq2a (syl orr aneq2a));
theorem ifptreq (h: $ G -> (p <-> ifp a p0 p1) $)
  (h0: $ G /\ a -> (p0 <-> q0) $) (h1: $ G /\ ~a -> (p1 <-> q1) $):
  $ G -> (p <-> ifp a q0 q1) $ = '(bitrd h (oreqid (aneq2da h0) (aneq2da h1)));
theorem ifppos2: $ b -> (ifp a b c <-> a \/ c) $ = '(syl6bb (ax_mp oreq2a bian1) (oreqid bian2 biidd));
theorem ifppos3: $ c -> (ifp a b c <-> ~a \/ b) $ = '(syl5bb (bicomi ifpnot) ifppos2);
theorem ifpneg3: $ ~c -> (ifp a b c <-> a /\ b) $ = '(syl bior2 (con3i anr));
theorem ifpneg2: $ ~b -> (ifp a b c <-> ~a /\ c) $ = '(syl5bb (bicomi ifpnot) (ifpneg3));

--| The constant `true`.
term tru: wff; prefix tru: $T.$ prec max;
--| `true` is true.
axiom itru: $ T. $;
--| The constant `false`.
def fal: wff = $ ~T. $; prefix fal: $F.$ prec max;

theorem trud (h: $ T. -> a $): $ a $ = '(ax_mp h itru);
theorem eqtru: $ (a <-> T.) <-> a $ = '(ax_mp bibi2 itru);
theorem notfal: $ ~F. $ = '(ax_mp notnot1 itru);
theorem efal: $ F. -> a $ = '(ax_mp absurdr itru);
theorem imfal: $ (a -> F.) <-> ~a $ = '(ax_mp biim2 notfal);
theorem eqfal: $ (a <-> F.) <-> ~a $ = '(ax_mp bibin2 notfal);
theorem neqfal: $ (~a <-> F.) <-> a $ = '(bitrii eqfal (bicomi notnot));

sort nat;
term al {x: nat} (p: wff x): wff; prefix al: $A.$ prec 41;
def ex {x: nat} (p: wff x): wff = $ ~(A. x ~p) $; prefix ex: $E.$ prec 41;
term eq (a b: nat): wff; infixl eq: $=$ prec 50;
axiom ax_gen {x: nat} (p: wff x): $ p $ > $ A. x p $;
axiom ax_4 {x: nat} (p q: wff x): $ A. x (p -> q) -> A. x p -> A. x q $;
theorem alim {x: nat} (p q: wff x): $ A. x (p -> q) -> A. x p -> A. x q $ = 'ax_4;
axiom ax_5 {x: nat} (p: wff): $ p -> A. x p $;
theorem ial {x: nat} (p: wff): $ p -> A. x p $ = 'ax_5;
axiom ax_6 (a: nat) {x: nat}: $ E. x x = a $;
axiom ax_7 (a b c: nat): $ a = b -> a = c -> b = c $;
theorem eqtr3: $ b = a -> b = c -> a = c $ = 'ax_7;
axiom ax_10 {x: nat} (p: wff x): $ ~(A. x p) -> A. x ~(A. x p) $;
axiom ax_11 {x y: nat} (p: wff x y): $ A. x A. y p -> A. y A. x p $;
theorem alcom {x y: nat} (p: wff x y): $ A. x A. y p -> A. y A. x p $ = 'ax_11;
axiom ax_12 {x: nat} (a: nat) (p: wff x): $ x = a -> p -> A. x (x = a -> p) $;

theorem alimi (a b: wff x) (h: $ a -> b $): $ A. x a -> A. x b $ = '(ax_mp ax_4 (ax_gen h));
theorem iald (a: wff) (b: wff x) (h: $ a -> b $): $ a -> A. x b $ = '(syl (alimi h) ax_5);
theorem eex (a: wff x) (b: wff) (h: $ a -> b $): $ E. x a -> b $ = '(con1i (iald (con3i h)));

theorem eqid: $ a = a $ = '(ax_mp (eex (ax_mp (imidm) (! ax_7 x a))) ax_6);
theorem eqcom: $ a = b -> b = a $ = '(mpi eqid ax_7);
theorem eqtr: $ a = b -> b = c -> a = c $ = '(syl ax_7 eqcom);
theorem eqtr2: $ a = b -> b = c -> c = a $ = '(syl6 eqcom eqtr);
theorem eqtr4: $ a = b -> c = b -> a = c $ = '(syl5 eqcom eqtr);
theorem eqcomb: $ a = b <-> b = a $ = '(iani eqcom eqcom);
theorem eqeq1: $ a = b -> (a = c <-> b = c) $ = '(ibid (syl eqtr eqcom) eqtr);
theorem eqeq2: $ b = c -> (a = b <-> a = c) $ = '(ibid (syl5 eqcom (syl eqtr2 eqcom)) (syl6 eqcom eqtr4));
theorem eqeq1d (h: $ G -> a = b $): $ G -> (a = c <-> b = c) $ = '(syl eqeq1 h);
theorem eqeq2d (h: $ G -> b = c $): $ G -> (a = b <-> a = c) $ = '(syl eqeq2 h);
theorem eqeqd (h1: $ G -> a = b $) (h2: $ G -> c = d $): $ G -> (a = c <-> b = d) $ = '(bitrd (eqeq1d h1) (eqeq2d h2));
theorem eqeq: $ a = b -> c = d -> (a = c <-> b = d) $ = '(exp (eqeqd anl anr));
theorem eqtr2d (h1: $ G -> a = b $) (h2: $ G -> b = c $): $ G -> c = a $ = '(sylc eqtr2 h1 h2);
theorem eqtr3d (h1: $ G -> b = a $) (h2: $ G -> b = c $): $ G -> a = c $ = '(sylc (syl eqtr eqcom) h1 h2);
theorem eqidd: $ G -> a = a $ = '(a1i eqid);
theorem eqcomi (h: $ a = b $): $ b = a $ = '(ax_mp eqcom h);
theorem eqcomd (h: $ G -> a = b $): $ G -> b = a $ = '(syl eqcom h);
theorem eqtrd (h1: $ G -> a = b $) (h2: $ G -> b = c $): $ G -> a = c $ = '(sylc eqtr h1 h2);
theorem eqtr4i (h1: $ a = b $) (h2: $ c = b $): $ a = c $ = '(ax_mp (ax_mp eqtr4 h1) h2);
theorem eqtr4d (h1: $ G -> a = b $) (h2: $ G -> c = b $): $ G -> a = c $ = '(sylc eqtr4 h1 h2);
theorem syl5eq (h1: $ a = b $) (h2: $ G -> b = c $): $ G -> a = c $ = '(mpi (eqcomi h1) (syl eqtr2 (eqcomd h2)));
theorem syl5eqr (h1: $ b = a $) (h2: $ G -> b = c $): $ G -> a = c $ = '(mpi h1 (syl eqtr2 (eqcomd h2)));
theorem syl6eq (h1: $ b = c $) (h2: $ G -> a = b $): $ G -> a = c $ = '(mpi (eqcomi h1) (syl eqtr4 h2));
theorem syl6eqr (h1: $ c = b $) (h2: $ G -> a = b $): $ G -> a = c $ = '(mpi h1 (syl eqtr4 h2));
theorem eqtr3g (h1: $ a = c $) (h2: $ b = d $) (h: $ G -> a = b $): $ G -> c = d $ = '(syl5eqr h1 (syl6eq h2 h));
theorem eqtr4g (h1: $ c = a $) (h2: $ d = b $) (h: $ G -> a = b $): $ G -> c = d $ = '(eqtr3g (eqcomi h1) (eqcomi h2) h);

theorem aleq (a b: wff x): $ A. x (a <-> b) -> (A. x a <-> A. x b) $ = '(ibid (syl ax_4 (alimi bi1)) (syl ax_4 (alimi bi2)));
theorem aleqi (a b: wff x) (h: $ a <-> b $): $ A. x a <-> A. x b $ = '(ax_mp aleq (ax_gen h));
theorem alimd (G: wff) (a b: wff x) (h: $ G -> a -> b $): $ G -> A. x a -> A. x b $ = '(syl ax_4 (syl (ax_mp ax_4 (ax_gen h)) ax_5));
theorem raleqi (p a b: wff x) (h: $ a <-> b $): $ A. x (p -> a) <-> A. x (p -> b) $ = '(aleqi (imeq2i h));
theorem al2imi (a b c: wff x) (h: $ a -> b -> c $): $ A. x a -> A. x b -> A. x c $ = '(syl ax_4 (alimi h));
theorem aleqd (G: wff) {x: nat} (a b: wff x) (h: $ G -> (a <-> b) $): $ G -> (A. x a <-> A. x b) $ = '(syl aleq (syl (alimi h) ax_5));
theorem ialda (G: wff) (a b: wff x) (h: $ G /\ a -> b $): $ G -> A. x (a -> b) $ = '(syl (alimi (exp h)) ax_5);
theorem alcomb (a: wff x y): $ A. x A. y a <-> A. y A. x a $ = '(ibii ax_11 ax_11);
theorem alan (a b: wff x): $ A. x (a /\ b) <-> A. x a /\ A. x b $ = '(ibii (iand (alimi anl) (alimi anr)) (imp_and (al2imi ian)));
theorem ralan (a b c: wff x): $ A. x (a -> b /\ c) <-> A. x (a -> b) /\ A. x (a -> c) $ = 
'(bitrii (aleqi imandi) alan);

theorem exim (a b: wff x): $ A. x (a -> b) -> E. x a -> E. x b $ = '();
theorem eximi (a b: wff x) (h: $ a -> b $): $ E. x a -> E. x b $ = '();
theorem exeq (a b: wff x): $ A. x (a <-> b) -> (E. x a <-> E. x b) $ = '();
theorem exeqd (G) {x} (a b: wff x) (h: $ G -> (a <-> b) $): $ G -> (E. x a <-> E. x b) $ = '();
theorem exeqi (a b: wff x) (h: $ a <-> b $): $ E. x a <-> E. x b $ = '();
theorem rexeqi (p a b: wff x) (h: $ a <-> b $): $ E. x (p /\ a) <-> E. x (p /\ b) $ = '();
theorem rexeqa (p a b: wff x) (h: $ p -> (a <-> b) $): $ E. x (p /\ a) <-> E. x (p /\ b) $ = '();
theorem rexeqd (p a b: wff x) (h: $ G -> (a <-> b) $): $ G -> (E. x (p /\ a) <-> E. x (p /\ b)) $ = '();
theorem rexeqda (p a b: wff x) (h: $ G /\ p -> (a <-> b) $): $ G -> (E. x (p /\ a) <-> E. x (p /\ b)) $ = '();
theorem iex (a: wff x): $ a -> E. x a $ = '();
theorem alanex (a b: wff x): $ A. x a /\ E. x b -> E. x (a /\ b) $ = '();
theorem exanal (a b: wff x): $ E. x a /\ A. x b -> E. x (a /\ b) $ = '();
theorem alnex (a: wff x): $ A. x ~a <-> ~(E. x a) $ = '();
theorem ngen (a: wff x) (h: $ ~a $): $ ~E. x a $ = '();
theorem alex (a: wff x): $ A. x a <-> ~(E. x ~a) $ = '();
theorem exnal (a: wff x): $ E. x ~a <-> ~(A. x a) $ = '();
theorem eal (a: wff x): $ A. x a -> a $ = '();
theorem albi: $ A. x a <-> a $ = '();
theorem exbi: $ E. x a <-> a $ = '();
theorem alan1 (b: wff x): $ A. x (a /\ b) <-> a /\ A. x b $ = '();
theorem alan2 (a: wff x): $ A. x (a /\ b) <-> A. x a /\ b $ = '();
theorem exor (a b: wff x): $ E. x (a \/ b) <-> E. x a \/ E. x b $ = '();
theorem eximd (G) (a b: wff x) (h: $ G -> a -> b $): $ G -> E. x a -> E. x b $ = '();
theorem excomb (a: wff x y): $ E. x E. y a <-> E. y E. x a $ = '();
theorem excom (a: wff x y): $ E. x E. y a -> E. y E. x a $ = '();
theorem nexi (a: wff x) (h: $ ~a $): $ ~E. x a $ = '();
theorem nexd (a: wff x) (h: $ G -> ~a $): $ G -> ~E. x a $ = '();
theorem exim1 (a: wff) (b: wff x): $ E. x (a -> b) -> (a -> E. x b) $ = '();